#Comments
comment-s
	[#[^\n]*\n] as comment 

#Tokens
cargon_token-s
	[[a-zA-Z_][a-zA-Z0-9_-]*] as NAME
	[\\n] as NEWLINE
	[.*] as WILD
	[.] as ANYCHAR
	[[-]?\\d+] as NUMBER
	[[^\\s]+] as NONSPACE
	"\"" as CHAR_QUOTE
	[[^\"]*] as quote
	"|" as PIPE
	"+" as PLUS
	"?" as OPTIONAL
	"*" as MANY
	"as" as AS
	"in" as IN
	"with" as WITH
	"are" as ARE
	"," as COMMA
	"from" as FROM
	"." as DOT
	"\t" as TAB
	"\n\t" as NEWTAB
	":" as COLON
	"silent" as SILENT
	"Braced" as BRACED
	"Imports" as IMPORTS
	"->" as ACCESS
	"list" as LIST
	"=" as EQUALSIGN
	"^" as CARROT
	"-" as DASH
	"\\d" as REGEX_NUMBER
	"\\s" as REGEX_WHITESPACE
	"\\." as REGEX_DOT
	"\\\"" as REGEX_QUOTE
	"\\\'" as REGEX_APOS
	"@" as PASS_SYMBOL
	"global" as GLOBAL

uote is silent
	CHAR_QUOTE quote CHAR_QUOTE 

#Braces
brace-s are Braced
	[(,)] as BRACE with definition  
	[[,]] as REGEX with regex
	[[,]] as REGEX_OPTION with CARROT as negate? regex_option*
	[(,)] as REGEX_GROUP with regex
regex_option is silent
	( ANYCHAR as left DASH ANYCHAR as right ) as range
	regex_special
	ANYCHAR as standAlone

altbrace-s are CharitableBraced
	[,\n] as RULE_PARAMETERS with rule_params? ( COMMA rule_params )*
	[,=] as EQUALS_RULE_PARAMETERS with rule_params? ( COMMA rule_params )*
rule_params is silent
	SILENT
	(PASS_SYMBOL NUMBER as passConstraint)? BRACED braced_parameters
	IMPORTS import_parameters
braced_parameters
	NONSPACE as left NONSPACE as right
import_parameters
	( uote | NAME as ruleName )+

#List Prototypes
rule-s are empty
list-s are empty
list_name-s are empty

#Rules
base is silent
	( NEWLINE | comments | list as list in lists | rule as rule in rules )+

rule is silent
	NAME as ruleName ( RULE_PARAMETERS | EQUALS_RULE_PARAMETERS )? ( NEWTAB | EQUALSIGN ) definition

list is silent
	NAME as listName in list_names GLOBAL? LIST (WITH NAME as listRuleName)? ( (COMMA| NEWTAB) uote )*


definition
	element as chain+ ( ( PIPE | NEWTAB ) definition ) as choice?

element
	atom ( AS NAME as newName)? ( IN NAME as listName)?

atom is silent
	( definition ( OPTIONAL | MANY | PLUS ) as option ) as multiple
	BRACE as braced
	uote as quoteToken
	REGEX as regexToken
	NAME as ruleToken

regex
	regex_element+
regex_element
	REGEX_OPTION as option ( OPTIONAL | MANY | PLUS ) as multiple?
	REGEX_GROUP as group ( OPTIONAL | MANY | PLUS ) as multiple?
	regex_special ( OPTIONAL | MANY | PLUS ) as multiple?
	ANYCHAR as character ( OPTIONAL | MANY | PLUS ) as multiple?
regex_special
	( REGEX_NUMBER | REGEX_DOT | REGEX_WHITESPACE | REGEX_QUOTE | REGEX_APOS )