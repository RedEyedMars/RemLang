#List structure
listnames
	[listnames] as listname
	[rules] as rule
	[tokens] as token
	[rulenames] as rulename
	[list_rules] as list_rule
	[braces] as brace
	[comments] as comment

#Comments
comments
	[#[^\n]*\n] as comment 

#Tokens
tokens
	[[|]] as PIPE
	[[+]] as PLUS
	[[?]] as OPTIONAL
	[[*]] as MANY
	[as] as AS
	[in] as IN
	[with] as WITH
	[are] as ARE
	[[ \t]+is[ \t]+silent] as SILENCE
	[\\t] as TAB
	[\\n] as NEWLINE
	[[a-zA-Z_-][a-zA-Z0-9_-]*] as NAME
	[[\\t ]+] as SPACES
	[.*] as WILD

#Braces
braces are Braced
	[(,)] as BRACE with definition
	[[,]] as SQUARE with WILD as regex
	[\",\"] as QUOTE with WILD as regex

#List Rules
list_rules are empty

#Rules
base is silent
	( SPACES | NEWLINE | comments | rule as rule in rules | list_rule as list_rule in list_rules )+

parameters
	(SPACES AS SPACES NAME as name )? (SPACES IN SPACES listnames as list )? (SPACES WITH SPACES definition as parameter)? SPACES? 

rule is silent
	NAME as rulename in rulenames ( SILENCE as silence )? SPACES? NEWLINE TAB definition


list_rule is silent		
	NAME as listname in listnames ( SPACES ARE SPACES NAME as listType )? SPACES? ( NEWLINE TAB ( ( QUOTE | SQUARE ) parameters ) as list_def )*

definition
	( atom atom* ) as chain ( SPACES? ( PIPE | NEWLINE TAB ) definition )? as choice

atom
	SPACES? element parameters

element	is silent
	( definition SPACES? ( OPTIONAL | MANY | PLUS ) as option ) as multiple
	BRACE as braced	
	terminal

terminal
	( rulename ) as ruleToken
	( token ) as token
	( listname ) as listToken
	( listnames ) as listsToken
	( brace ) as braceToken